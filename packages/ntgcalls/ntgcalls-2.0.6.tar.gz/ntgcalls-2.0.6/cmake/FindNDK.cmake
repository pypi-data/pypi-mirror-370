if (ANDROID_ABI)
    set(NDK_DIR ${DEPS_DIR}/ndk)
    set(NDK_SRC ${NDK_DIR}/src)

    if (NOT ANDROID_NDK)
        GetProperty("version.ndk" NDK_VERSION)
        message(STATUS "android-ndk ${NDK_VERSION}")
        if (NOT EXISTS ${NDK_SRC})
            if (NOT LINUX)
                message(FATAL_ERROR "Only Linux is supported for Android build")
            endif ()
            DownloadProject(
                URL https://dl.google.com/android/repository/android-ndk-${NDK_VERSION}-linux.zip
                DOWNLOAD_DIR ${NDK_DIR}/download
                SOURCE_DIR ${NDK_SRC}
            )
        endif ()
        GetProperty("version.sdk_compile" ANDROID_NATIVE_API_LEVEL)
    endif ()

    set(ANDROID_NATIVE_API_LEVEL    ${ANDROID_NATIVE_API_LEVEL})
    set(ANDROID_PLATFORM            ${ANDROID_NATIVE_API_LEVEL})
    set(ANDROID_STL                 none)
    set(ANDROID_CPP_FEATURES        exceptions rtti)
    set(CMAKE_ANDROID_EXCEPTIONS    ON)

    if (NOT ANDROID_NDK)
        set(ANDROID_NDK OFF)
    endif ()

    include(${NDK_SRC}/build/cmake/android.toolchain.cmake)
    set(ANDROID_CLANG_LIBRARY_FOLDER "${ANDROID_TOOLCHAIN_ROOT}/lib/clang/")
    file(GLOB _clang_root_dirs "${ANDROID_CLANG_LIBRARY_FOLDER}*")
    list(GET _clang_root_dirs 0 ANDROID_CLANG_LIBRARY_FOLDER)
    if (ANDROID_ABI STREQUAL "arm64-v8a")
        set(ARCH_NAME_ANDROID "aarch64")
    elseif (ANDROID_ABI STREQUAL "armeabi-v7a")
        set(ARCH_NAME_ANDROID "arm")
    elseif (ANDROID_ABI STREQUAL "x86_64")
        set(ARCH_NAME_ANDROID "x86_64")
    elseif (ANDROID_ABI STREQUAL "x86")
        set(ARCH_NAME_ANDROID "i386")
    else ()
        message(FATAL_ERROR "Unsupported Android ABI: ${ANDROID_ABI}")
    endif ()
    set(ANDROID_CLANG_LIBRARY_FOLDER "${ANDROID_CLANG_LIBRARY_FOLDER}/lib/linux/${ARCH_NAME_ANDROID}")
    set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -L${ANDROID_CLANG_LIBRARY_FOLDER}")
    set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -L${ANDROID_CLANG_LIBRARY_FOLDER}")
endif ()