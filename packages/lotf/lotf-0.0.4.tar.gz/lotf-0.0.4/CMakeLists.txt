cmake_minimum_required(VERSION 3.15...3.26)

project(lotf LANGUAGES CXX)

if (NOT SKBUILD)
  message(WARNING "\
  This CMake file is meant to be executed using 'scikit-build'. Running
  it directly will almost certainly not produce the desired result. If
  you are a user trying to install this package, please use the command
  below, which will install all necessary build dependencies, compile
  the package in an isolated environment, and then install it.
  =====================================================================
   $ pip install .
  =====================================================================
  If you are a software developer, and this is your own package, then
  it is usually much more efficient to install the build dependencies
  in your environment once and use the following command that avoids
  a costly creation of a new virtual environment at every compilation:
  =====================================================================
   $ pip install nanobind scikit-build-core[pyproject]
   $ pip install --no-build-isolation -ve .
  =====================================================================
  You may optionally add -Ceditable.rebuild=true to auto-rebuild when
  the package is imported. Otherwise, you need to re-run the above
  after editing C++ files.")
endif()

# Try to import all Python components potentially needed by nanobind
find_package(Python 3.10
  REQUIRED COMPONENTS Interpreter Development.Module
  OPTIONAL_COMPONENTS Development.SABIModule)

# Import nanobind through CMake's find_package mechanism
find_package(nanobind CONFIG REQUIRED)

# Try to find Boost headers for better performance
# Check common Boost installation paths
# [Note] We can use boost on Linux without this description,
# but it was necessary when creating wheels for Mac or Windows.
# If Boost is not available, this description is simply ignored.

set(BOOST_SEARCH_PATHS
    /opt/homebrew/include          # macOS Homebrew (Apple Silicon)
    /usr/local/include             # macOS Homebrew (Intel) / Linux
    /usr/include                   # System package managers
    $ENV{VCPKG_INSTALLATION_ROOT}/installed/x64-windows/include  # Windows vcpkg
    C:/vcpkg/installed/x64-windows/include  # Windows vcpkg (fallback)
)

foreach(BOOST_PATH ${BOOST_SEARCH_PATHS})
    if(EXISTS "${BOOST_PATH}/boost/unordered/unordered_flat_map.hpp")
        message(STATUS "Found Boost headers at: ${BOOST_PATH}")
        include_directories(${BOOST_PATH})
        break()
    endif()
endforeach()

# We are now ready to compile the actual extension module
nanobind_add_module(
  # Name of the extension
  lotf_ext

  # Target the stable ABI for Python 3.12+, which reduces
  # the number of binary wheels that must be built. This
  # does nothing on older Python versions
  STABLE_ABI

  # Build libnanobind statically and merge it into the
  # extension (which itself remains a shared library)
  #
  # If your project builds multiple extensions, you can
  # replace this flag by NB_SHARED to conserve space by
  # reusing a shared libnanobind across libraries
  NB_STATIC

  # Source code goes here
  src/lotf_ext.cpp
)

# Install directive for scikit-build-core
install(TARGETS lotf_ext LIBRARY DESTINATION lotf)
