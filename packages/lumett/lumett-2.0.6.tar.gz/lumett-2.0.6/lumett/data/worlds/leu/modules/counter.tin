#nop --
#nop Class Definitions
#nop --

#var counter-description {Utilità per i Contatori}
#var counter-help {
Sistema di contatori per tenere traccia di valori:\n
FUNZIONI PRINCIPALI:\n
• counter_increment <nome> [valore]\n
  Incrementa contatore di 1 o del valore specificato\n
• counter_reset <nome> [valore_iniziale]\n
  Resetta contatore a 0 o al valore specificato\n
• @counter_value{nome} - funzione\n
  Restituisce valore del contatore (0 se non esiste)\n
COME FUNZIONA:\n
1. I contatori sono memorizzati in _COUNTERS[nome]\n
2. Se un contatore non esiste, viene inizializzato\n
3. I contatori persistono per tutta la sessione\n
4. Utili per statistiche, tracking, etc.\n
ESEMPI:\n
counter_reset morti 0\n
counter_increment morti\n
counter_increment esperienza 1000\n;
#showme Morti: @counter_value{morti}\n
NOTE:\n
• I contatori non vengono salvati tra le sessioni\n
• Valori possono essere decimali\n
• Errore se si tenta di leggere contatore inesistente
}

#nop --
#nop Modloader Stuff
#nop --

#alias counter-register {
	#if {@isloaded{modloader}} {

		register_module counter
	} {
		#class counter kill
	}
}

#tab counter_reset
#alias {counter_reset %1 %2} {
    #if {&_COUNTERS} {
        #var _COUNTERS[%1] {%2}
    } {
        init {_COUNTERS[%1]} counter {%2}
    }
} {2}
#alias {counter_reset %1} {
    #if {&_COUNTERS} {
        #var _COUNTERS[%1] 0
    } {
        init _COUNTERS[%1] counter 0
    }
} {3}

#tab counter_increment
#alias {counter_increment %1 %2} {
    #if {&_COUNTERS[%1]} {
        #math _COUNTERS[%1] {$_COUNTERS[%1] + %2}
    } {
        init _COUNTERS[%1] counter %2;
    }
} {3}
#alias {counter_increment %1} {
    #if {&_COUNTERS[%1]} {
        #math _COUNTERS[%1] {$_COUNTERS[%1] + 1}
    } {
        init _COUNTERS[%1] counter 1;
    }
} {4}

#alias {counter_increment} {
    #showme Usage: counter_increment <counter name> <value>
} {5}

#function {counter_value} {
    #if {&_COUNTERS[%1]} {
        #return $_COUNTERS[%1]
    } {
        err Counter '%1' not initialized.;
        #return 0
    }
}


