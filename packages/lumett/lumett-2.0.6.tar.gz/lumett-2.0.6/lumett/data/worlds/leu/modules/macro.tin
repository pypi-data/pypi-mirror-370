#nop --
#nop Class Definitions
#nop --

#var macro-description {Funzioni per le Macro}
#var macro-help {
Sistema di registrazione e riproduzione macro:\n
FUNZIONI PRINCIPALI:\n
• recmacro <nome> - inizia registrazione macro\n
• recmacro (senza parametri) - ferma registrazione\n
• playmacro <nome> - riproduce una macro\n
• stopmacro - ferma riproduzione in corso\n
COME FUNZIONA:\n
1. recmacro cattura tutti i comandi inviati al MUD\n
2. Calcola i tempi di attesa tra i comandi\n
3. Salva sequenza con timing preciso\n
4. playmacro riproduce con gli stessi ritardi\n
CARATTERISTICHE:\n
• Timing automatico basato su timestamp\n
• Conteggio comandi registrati\n
• Possibilità di fermare riproduzione\n
• Macro persistenti per la sessione\n
ESEMPI:\n
recmacro combattimento\n
(esegui sequenza di comandi)\n
recmacro (ferma registrazione)\n
playmacro combattimento (riproduce)\n
VARIABILI INTERNE:\n
• record_time - timestamp inizio registrazione\n
• record_name - nome macro corrente\n
• record_cnt - contatore comandi\n
NOTE:\n
• Una sola registrazione alla volta\n
• I ritardi sono calcolati in microsecondi\n
• Le macro non vengono salvate tra sessioni
}

#nop --
#nop Modloader Stuff
#nop --

#alias macro-register {
	#nop need to check: events;
	#if {@isloaded{functions}} {
		register_module macro
	} {
		fail_module macro
	}
}

#nop -------------------------------------------------------------------------
#nop File di gestione delle registrazioni
#nop -------------------------------------------------------------------------

#VARIABLE {record_time} {0}

#ALIAS {recmacro}
{
     #if {$record_time == 0}
     {
          #if {"%0" == ""}
          {
               #showme {<118>Inserici il nome della registrazione};
               #break
          };
          #else
          {
               #format record_name %0
          };
          #format record_time %U;
          #format $record_name %s;
          #format record_cnt 0;
          #ALIAS {%%0}
          {
               #SEND %%0;#format record_curr %U;#math record_diff ($record_curr - $record_time) / 1000000.00;#list $record_name ins -1 #delay $record_diff %%0;#math record_cnt $record_cnt + 1;
          }{9};
          #showme {<118>Stai registrando i comandi mandati al mud.}
     };
     #else
     {
          #format record_curr %U;
          #math record_diff ($record_curr - $record_time) / 1000000.00;
          #list {$record_name} {ins} {1} {#showme <118>Riproduzione di '$record_name' per $record_cnt comandi e della durata di $record_diff secondi};
          #showme {<118>Hai registrato $record_cnt comandi durante $record_diff secondi a $record_name};
          #format record_time 0;
          #UNALIAS %%0
     };
}

#ALIAS {playmacro}
{
     #if {"%0" == ""}
     {
          #showme {<118>Inserisci il nome di una registrazione da riprodurre}
     };
     #elseif {&{%0} == 0}
     {
          #showme {<118>Non c'e' una registrazione $%0}
     };
     #else
     {
          #foreach {$%0} {temprec}
          {
               $temprec
          };
     };
}

#ALIAS {stopmacro}
{
     #showme {<118>Fermo la ripdoduzione}; #kill delays
}
