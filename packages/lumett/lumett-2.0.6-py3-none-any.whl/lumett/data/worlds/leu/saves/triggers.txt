#CLASS {trigger_data} OPEN

#nop -- MAP MOVEMENT TRIGGERS --;
#ACTION {Non puoi andare da quella parte.}
{
    #map undo
}

#ACTION {%1 ti impedisce il passaggio.}
{
    #map undo
}

#ACTION {La porta e` chiusa.}
{
    #map undo
}

#ACTION {Non puoi! Stai combattendo per la tua vita!}
{
    #map undo
}

#ACTION {[il Guardiano della Gilda] ti dice a 'Dove credi di andare tu? L'ingresso e` riservato!'}
{
    #map undo
}

#nop -- COMUNICATION TRIGGERS --;

#nop eq trigger;
#ACTION {[%1] <%2>%*[%4|%5|%6] %7$}
{
    #if {$gui_ingame[ui] == 3 | 4} {
        #var eq {};
    	#FORMAT tmpeq {%s %s %s [%s,%s,%s] %s %s} %1 %2 %3 %4 %5 %6 %7 %8;
    	#nop call function bar_side_show from bar_side file module;
    	#nop bar_side_show eq $tmpeq;
   	    #format menueq {%s} {\e]68;1;SEND;rem $tmpeq\a\e[4;24m%0\e[24m};
    	bar_side_show eq $menueq
    }
}

#ACTION {^{\[.*\]} dice al gruppo {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show grp %1 dice al gruppo: %3;
    bar_comm_show all %1 dice al gruppo: %2
}

#ACTION {^Tu dici al gruppo {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show grp Dici al gruppo %1;
    bar_comm_show all Dici al gruppo %1  
}

#ACTION {^Tu {?:chiedi|dici|sussurri} {?:a|ai|al|alla|alle|agli} {.*} {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show msg Tu dici %1 %3 %2;
    bar_comm_show all Tu dici %1 %2 %3   
}

#ACTION {^{\[.*\]} {ti} {?:manda il messaggio|manda il pensiero|chiede|dice|sussurra} {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show msg %1 dice %3;
    bar_comm_show all %1 dice %3
}

#ACTION {^{Mandi} a {.*} il {?:pensiero|messaggio} {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show msg Dici a %2 %3;
    bar_comm_show all Dici a %2 %3
}

#ACTION {^{\[.*\]} dice {?:a |ai |al |all'|alla |alle |agli }{.*} {\'.*\'}$}
{
    #nop call function bar_comm_show from bar_comm file module;   
    bar_comm_show gld %1 dice: %3;
    bar_comm_show all %1 dice: %3
}

#nop -- RIP TRIGGERS --;
#ACTION {Hai ucciso %1 %2! R.I.P}
{
    #variable deadmob %2;
    #if {&targets[%1]} {
      target %2
    };
    #nop call bar_side_show alias from bar_side module file;
    #if {$gui_ingame[ui] == 3 | 4} {
       #FORMAT tmpdeadmob {%s} {\e]68;2;TAB;rip\a\e[4m$deadmob\e[24m};
       bar_side_show rip Hai ucciso [ %1 $tmpdeadmob ];
       #unvariable tmpdeadmob
    };       
}

#ACTION {Ottieni %1 monete d'oro e ricevi %2 punti esperienza.}
{
    #nop update the gains vars in alias module file for rip function;
    #math {gains[$deadmob][mobexp]} {$gains[$deadmob][mobexp] + %2};
    #math {gains[$deadmob][mobcnt]} {$gains[$deadmob][mobcnt] + 1}  
}

#ACTION {Ricevi %1 punti esperienza.}
{
    #nop update the gains vars in alias module file for rip function;
    #math {gains[$deadmob][mobexp]} {$gains[$deadmob][mobexp] + %1};
    #math {gains[$deadmob][mobcnt]} {$gains[$deadmob][mobcnt] + 1}
}

#CLASS {trigger_data} CLOSE
