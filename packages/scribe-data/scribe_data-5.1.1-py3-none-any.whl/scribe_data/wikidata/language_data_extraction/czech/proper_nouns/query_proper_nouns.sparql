# tool: scribe-data
# All Czeck (Q9056) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?properNoun
  ?nominativePlural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q9056 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?properNoun .

  # MARK: Nominative Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
