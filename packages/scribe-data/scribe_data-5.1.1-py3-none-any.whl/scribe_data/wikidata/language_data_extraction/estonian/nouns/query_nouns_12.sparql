# tool: scribe-data
# All Estonian (Q9072) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?partitiveSingular
  ?partitivePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitiveSingularForm .
    ?partitiveSingularForm ontolex:representation ?partitiveSingular ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?partitivePluralForm .
    ?partitivePluralForm ontolex:representation ?partitivePlural ;
      wikibase:grammaticalFeature wd:Q857325, wd:Q146786 .
  }
}
