# tool: scribe-data
# All English (Q1860) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?infinitive
  ?simplePresent
  ?simplePast
  ?presentParticiple
  ?pastParticiple
  ?simplePresentThirdPersonSingular

WHERE {
  # MARK: Infinitive

  ?lexeme dct:language wd:Q1860 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?infinitive .

  # MARK: Simple Present

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentForm .
    ?simplePresentForm ontolex:representation ?simplePresent ;
      wikibase:grammaticalFeature wd:Q3910936 .
      FILTER NOT EXISTS { ?simplePresentForm wikibase:grammaticalFeature wd:Q51929074 . }
      FILTER NOT EXISTS { ?simplePresentForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePresentForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePresent) = "en")
  }

  # MARK: Simple Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePastForm .
    ?simplePastForm ontolex:representation ?simplePast ;
      wikibase:grammaticalFeature wd:Q1392475 .
      FILTER NOT EXISTS { ?simplePastForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePastForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePast) = "en")
  }

  # MARK: Present Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentParticipleForm .
    ?presentParticipleForm ontolex:representation ?presentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
      FILTER NOT EXISTS { ?presentParticipleForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?presentParticipleForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?presentParticiple) = "en")
  }

  # MARK: Past Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q1230649 .
      FILTER NOT EXISTS { ?pastParticipleForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?pastParticipleForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?pastParticiple) = "en")
  }

  # MARK: Third-person Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?simplePresentThirdPersonSingularForm .
    ?simplePresentThirdPersonSingularForm ontolex:representation ?simplePresentThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q3910936, wd:Q51929074, wd:Q110786 .
      FILTER NOT EXISTS { ?simplePresentThirdPersonSingularForm wdt:P6191 wd:Q181970 . }
      FILTER NOT EXISTS { ?simplePresentThirdPersonSingularForm wikibase:grammaticalFeature wd:Q126473 . }
      FILTER(LANG(?simplePresentThirdPersonSingular) = "en")
  }
}
