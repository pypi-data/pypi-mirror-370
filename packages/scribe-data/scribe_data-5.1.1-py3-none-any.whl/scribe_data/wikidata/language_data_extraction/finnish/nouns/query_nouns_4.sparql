# tool: scribe-data
# All Finnish (Q1412) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?noun
  ?comitative
  ?genitiveSingular
  ?genitivePlural
  ?illativeSingular
  ?illativePlural
  ?comitativePlural

WHERE {
  ?lexeme dct:language wd:Q1412 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?noun ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativeForm .
    ?comitativeForm ontolex:representation ?comitative ;
      wikibase:grammaticalFeature wd:Q838581 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitivePluralForm .
    ?genitivePluralForm ontolex:representation ?genitivePlural ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativeSingularForm .
    ?illativeSingularForm ontolex:representation ?illativeSingular ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativePluralForm .
    ?illativePluralForm ontolex:representation ?illativePlural ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comitativePluralForm .
    ?comitativePluralForm ontolex:representation ?comitativePlural ;
      wikibase:grammaticalFeature wd:Q838581, wd:Q146786 .
  }
}
