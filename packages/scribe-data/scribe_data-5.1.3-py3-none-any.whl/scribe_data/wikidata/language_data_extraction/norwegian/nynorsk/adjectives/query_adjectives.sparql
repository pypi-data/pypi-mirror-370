# tool: scribe-data
# All Nynorsk Norwegian (Q25164) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

# Note: This query is for Nynorsk (Q25164) rather than Bokm√•l (Q25167).

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?plural
  ?definiteSingular
  ?neuterIndefiniteSingular
  ?feminineMasculineIndefiniteSingular

WHERE {
  ?lexeme dct:language wd:Q25164 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  # MARK: Definite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularForm .
    ?definiteSingularForm ontolex:representation ?definiteSingular ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786 .
  }

  # MARK: Neuter Indefinite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularForm .
    ?neuterIndefiniteSingularForm ontolex:representation ?neuterIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786 .
  }

  # MARK: Common Indefinite

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularForm .
    ?feminineMasculineIndefiniteSingularForm ontolex:representation ?feminineMasculineIndefiniteSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786 .
  }
}
