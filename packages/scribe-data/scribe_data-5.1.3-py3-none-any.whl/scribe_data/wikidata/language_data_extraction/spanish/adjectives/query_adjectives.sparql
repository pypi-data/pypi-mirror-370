# tool: scribe-data
# All Spanish (Q1321) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective

  ?feminineSingular
  ?femininePlural
  ?masculineSingular
  ?masculinePlural

  ?feminineSingularSuperlative
  ?femininePluralSuperlative
  ?masculineSingularSuperlative
  ?masculinePluralSuperlative

WHERE {
  ?lexeme dct:language wd:Q1321 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .

  # MARK: Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
      FILTER NOT EXISTS {
        ?feminineSingularForm wikibase:grammaticalFeature wd:Q1817208 .
      }
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralForm .
    ?femininePluralForm ontolex:representation ?femininePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
      FILTER NOT EXISTS {
        ?femininePluralForm wikibase:grammaticalFeature wd:Q1817208 .
      }
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
      FILTER NOT EXISTS {
        ?masculineSingularForm wikibase:grammaticalFeature wd:Q1817208 .
      }
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
      FILTER NOT EXISTS {
        ?masculinePluralForm wikibase:grammaticalFeature wd:Q1817208 .
      }
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularSuperlativeForm .
    ?feminineSingularSuperlativeForm ontolex:representation ?feminineSingularSuperlative ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralSuperlativeForm .
    ?femininePluralSuperlativeForm ontolex:representation ?femininePluralSuperlative ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1817208 .
  }

  # MARK: Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularSuperlativeForm .
    ?masculineSingularSuperlativeForm ontolex:representation ?masculineSingularSuperlative ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralSuperlativeForm .
    ?masculinePluralSuperlativeForm ontolex:representation ?masculinePluralSuperlative ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q1817208 .
  }
}
