# tool: scribe-data
# All Hebrew (Q9288) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?adjective
  ?feminineSingular
  ?femininePlural
  ?masculineSingular
  ?masculinePlural
  ?feminineSingularConstruct
  ?femininePluralConstruct
  ?masculineSingularConstruct
  ?masculinePluralConstruct

WHERE {
  ?lexeme dct:language wd:Q9288 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective .
    FILTER(lang(?adjective) = "he")

  # MARK: Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
    FILTER NOT EXISTS {
      ?feminineSingularForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?feminineSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralForm .
    ?femininePluralForm ontolex:representation ?femininePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
    FILTER NOT EXISTS {
      ?femininePluralForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?femininePlural) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
    FILTER NOT EXISTS {
      ?masculineSingularForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?masculineSingular) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
    FILTER NOT EXISTS {
      ?masculinePluralForm wikibase:grammaticalFeature wd:Q1641446 .
    }
    FILTER(lang(?masculinePlural) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularConstructForm .
    ?feminineSingularConstructForm ontolex:representation ?feminineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786, wd:Q1641446 .
      FILTER(lang(?feminineSingularConstruct) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralConstructForm .
    ?femininePluralConstructForm ontolex:representation ?femininePluralConstruct ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786, wd:Q1641446 .
    FILTER(lang(?femininePluralConstruct) = "he")
  }

  # MARK: Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularConstructForm .
    ?masculineSingularConstructForm ontolex:representation ?masculineSingularConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786, wd:Q1641446 .
    FILTER(lang(?masculineSingularConstruct) = "he")
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralConstructForm .
    ?masculinePluralConstructForm ontolex:representation ?masculinePluralConstruct ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786, wd:Q1641446 .
      FILTER(lang(?masculinePluralConstruct) = "he")
  }
}
