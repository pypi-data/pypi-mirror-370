# tool: scribe-data
# All Hindi Hindustani (Q11051) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

# Note: We need to filter for "hi" to remove Urdu (ur) words.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  ?plural
  ?gender

WHERE {
  ?lexeme dct:language wd:Q11051 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?singular .
    FILTER(lang(?singular) = "hi")

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
      FILTER(lang(?plural) = "hi")
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
