# tool: scribe-data
# All Danish (Q9035) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb

  ?imperative

  ?presentParticiple
  ?pastParticiple

  ?passiveInfinitive
  ?passivePresent
  ?passivePreterite

  ?activeInfinitive
  ?activePresent
  ?activePreterite

WHERE {
  ?lexeme dct:language wd:Q9035 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Imperative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  # MARK: Participles

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentParticipleForm .
    ?presentParticipleForm ontolex:representation ?presentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  # MARK: Passive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passiveInfinitiveForm .
    ?passiveInfinitiveForm ontolex:representation ?passiveInfinitive ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePresentForm .
    ?passivePresentForm ontolex:representation ?passivePresent ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?passivePreteriteForm .
    ?passivePreteriteForm ontolex:representation ?passivePreterite ;
      wikibase:grammaticalFeature wd:Q1194697, wd:Q442485 .
  }

  # MARK: Active

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activeInfinitiveForm .
    ?activeInfinitiveForm ontolex:representation ?activeInfinitive ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q179230 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePresentForm .
    ?activePresentForm ontolex:representation ?activePresent ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?activePreteriteForm .
    ?activePreteriteForm ontolex:representation ?activePreterite ;
      wikibase:grammaticalFeature wd:Q1317831, wd:Q442485 .
  }
}
