# tool: scribe-data
# All Punjabi (Q58635) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?intransitivePhase
  ?feminineThirdPersonSingular
  ?futureFirstPersonSingular

WHERE {
  ?lexeme dct:language wd:Q58635 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  # Note: We need to filter for pa to remove gurmukhi (pa) words.
  FILTER(lang(?verb) = "pa")

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?intransitivePhaseForm .
    ?intransitivePhaseForm ontolex:representation ?intransitivePhase ;
      wikibase:grammaticalFeature wd:Q113330736 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineThirdPersonSingularForm .
    ?feminineThirdPersonSingularForm ontolex:representation ?feminineThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureFirstPersonSingularForm .
    ?futureFirstPersonSingularForm ontolex:representation ?futureFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q501405, wd:Q21714344, wd:Q110786 .
  }
}
