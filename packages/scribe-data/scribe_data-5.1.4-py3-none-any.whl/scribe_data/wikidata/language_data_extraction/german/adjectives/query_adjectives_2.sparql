# tool: scribe-data
# All German (Q188) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?positive
  ?nominativeNeuter
  ?nominativeSingular
  ?nominativePlural

WHERE {
  ?lexeme dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?positiveForm .
    ?positiveForm ontolex:representation ?positive ;
      wikibase:grammaticalFeature wd:Q3482678 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeNeuterForm .
    ?nominativeNeuterForm ontolex:representation ?nominativeNeuter ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q1775461 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativeSingularForm .
    ?nominativeSingularForm ontolex:representation ?nominativeSingular ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?nominativePluralForm .
    ?nominativePluralForm ontolex:representation ?nominativePlural ;
      wikibase:grammaticalFeature wd:Q131105, wd:Q146786 .
  }
}
