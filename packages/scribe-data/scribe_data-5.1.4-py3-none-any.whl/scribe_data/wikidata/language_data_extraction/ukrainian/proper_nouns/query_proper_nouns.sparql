# tool: scribe-data
# All Ukrainian (Q8798) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?nominativeSingular
  ?genitiveSingular
  ?dativeSingular
  ?accusativeSingular
  ?instrumentalSingular
  ?locativeSingular
  ?vocativeSingular
  ?gender

WHERE {
  ?lexeme dct:language wd:Q8798 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?nominativeSingular ;
    schema:dateModified ?lastModified .

  # MARK: Genitive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?genitiveSingularForm .
    ?genitiveSingularForm ontolex:representation ?genitiveSingular ;
      wikibase:grammaticalFeature wd:Q146233, wd:Q110786 .
  }

  # MARK: Dative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?dativeSingularForm .
    ?dativeSingularForm ontolex:representation ?dativeSingular ;
      wikibase:grammaticalFeature wd:Q145599, wd:Q110786 .
  }

  # MARK: Accusative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?accusativeSingularForm .
    ?accusativeSingularForm ontolex:representation ?accusativeSingular ;
      wikibase:grammaticalFeature wd:Q146078, wd:Q110786 .
  }

  # MARK: Instrumental

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?instrumentalSingularForm .
    ?instrumentalSingularForm ontolex:representation ?instrumentalSingular ;
      wikibase:grammaticalFeature wd:Q192997, wd:Q110786 .
  }

  # MARK: Locative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?locativeSingularForm .
    ?locativeSingularForm ontolex:representation ?locativeSingular ;
      wikibase:grammaticalFeature wd:Q202142, wd:Q110786 .
  }

  # MARK: Vocative Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?vocativeSingularForm .
    ?vocativeSingularForm ontolex:representation ?vocativeSingular ;
      wikibase:grammaticalFeature wd:Q185077, wd:Q110786 .
  }

  # MARK: Gender

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
