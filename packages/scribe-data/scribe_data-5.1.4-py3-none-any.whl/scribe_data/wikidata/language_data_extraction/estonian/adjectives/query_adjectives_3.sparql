# tool: scribe-data
# All Estonian (Q9072) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?illativeSingular
  ?illativePlural
  ?inessiveSingular
  ?inessivePlural

WHERE {
  ?lexeme dct:language wd:Q9072 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .

  # MARK: Illative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativeSingularForm .
    ?illativeSingularForm ontolex:representation ?illativeSingular ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?illativePluralForm .
    ?illativePluralForm ontolex:representation ?illativePlural ;
      wikibase:grammaticalFeature wd:Q474668, wd:Q146786 .
  }

  # MARK: Inessive

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessiveSingularForm .
    ?inessiveSingularForm ontolex:representation ?inessiveSingular ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?inessivePluralForm .
    ?inessivePluralForm ontolex:representation ?inessivePlural ;
      wikibase:grammaticalFeature wd:Q282031, wd:Q146786 .
  }

}
