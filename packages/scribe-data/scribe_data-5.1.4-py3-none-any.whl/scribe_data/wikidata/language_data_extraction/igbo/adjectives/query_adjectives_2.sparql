# tool: scribe-data
# All Igbo (Q33578) adjectives (Q34698) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?adjective
  ?presentContinuous
  ?combinedPresentParticiple
  ?combinedPastParticiple
  ?comparative
  ?superlative
  ?singularPlural

WHERE {
  ?lexeme dct:language wd:Q33578 ;
    wikibase:lexicalCategory wd:Q34698 ;
    wikibase:lemma ?adjective ;
    schema:dateModified ?lastModified .
    
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousForm .
    ?presentContinuousForm ontolex:representation ?presentContinuous ;
      wikibase:grammaticalFeature wd:Q7240943 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPresentParticipleForm .
    ?combinedPresentParticipleForm ontolex:representation ?combinedPresentParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?combinedPastParticipleForm .
    ?combinedPastParticipleForm ontolex:representation ?combinedPastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?comparativeForm .
    ?comparativeForm ontolex:representation ?comparative ;
      wikibase:grammaticalFeature wd:Q14169499 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?superlativeForm .
    ?superlativeForm ontolex:representation ?superlative ;
      wikibase:grammaticalFeature wd:Q1817208 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularPluralForm .
    ?singularPluralForm ontolex:representation ?singularPlural ;
      wikibase:grammaticalFeature wd:Q110786, wd:Q146786 .
  }
}
