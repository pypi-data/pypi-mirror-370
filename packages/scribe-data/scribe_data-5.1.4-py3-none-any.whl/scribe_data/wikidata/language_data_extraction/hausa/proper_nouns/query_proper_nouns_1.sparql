# tool: scribe-data
# All Hausa (Q56475) proper nouns (Q147276) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?singular
  ?gender

WHERE {
  ?lexeme dct:language wd:Q56475 ;
    wikibase:lexicalCategory wd:Q147276 ;
    wikibase:lemma ?singular ;
    schema:dateModified ?lastModified .
    FILTER(lang(?singular) = "ha")

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
