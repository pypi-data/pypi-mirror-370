# tool: scribe-data
# All Dagbani (Q32238) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?verb
  ?imperative
  ?presentContinuous
  ?past
  ?future

WHERE {
  ?lexeme dct:language wd:Q32238 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb .

  # MARK: Imperative

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeForm .
    ?imperativeForm ontolex:representation ?imperative ;
      wikibase:grammaticalFeature wd:Q22716 .
  }

  # MARK: Present Continuous

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentContinuousForm .
    ?presentContinuousForm ontolex:representation ?presentContinuous ;
      wikibase:grammaticalFeature wd:Q7240943 .
  }

  # MARK: Past

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastForm .
    ?pastForm ontolex:representation ?past ;
      wikibase:grammaticalFeature wd:Q1994301 .
  }

  # MARK: Future

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?futureForm .
    ?futureForm ontolex:representation ?future ;
      wikibase:grammaticalFeature wd:Q501405 .
  }
}
