# tool: scribe-data
# All Basque (Q8752) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?absIndefinite
  ?absolutiveSingular
  ?absolutivePlural

WHERE {
  ?lexeme dct:language wd:Q8752 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?absIndefinite .

  # MARK: Absolutive Singular

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absolutiveSingularForm .
    ?absolutiveSingularForm ontolex:representation ?absolutiveSingular ;
      wikibase:grammaticalFeature wd:Q332734, wd:Q110786 .
  }

  # MARK: Absolutive Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?absolutivePluralForm .
    ?absolutivePluralForm ontolex:representation ?absolutivePlural ;
      wikibase:grammaticalFeature wd:Q332734, wd:Q146786 .
  }
}
