# tool: scribe-data
# All Polish (Q809) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?verb
  ?conditionalPresent
  ?conditionalFuture
  ?imperativeFirstPersonSingular
  ?imperativeFirstPersonPlural
  ?imperativeThirdPersonSingular
  ?imperativeThirdPersonPlural

WHERE {
  ?lexeme dct:language wd:Q809 ;
    wikibase:lexicalCategory wd:Q24905 ;
    wikibase:lemma ?verb ;
    schema:dateModified ?lastModified .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalPresentForm .
    ?conditionalPresentForm ontolex:representation ?conditionalPresent ;
      wikibase:grammaticalFeature wd:Q625581, wd:Q192613 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?conditionalFutureForm .
    ?conditionalFutureForm ontolex:representation ?conditionalFuture ;
      wikibase:grammaticalFeature wd:Q625581, wd:Q501405 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeFirstPersonSingularForm .
    ?imperativeFirstPersonSingularForm ontolex:representation ?imperativeFirstPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q21714344, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeFirstPersonPluralForm .
    ?imperativeFirstPersonPluralForm ontolex:representation ?imperativeFirstPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q21714344, wd:Q146786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeThirdPersonSingularForm .
    ?imperativeThirdPersonSingularForm ontolex:representation ?imperativeThirdPersonSingular ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929074, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?imperativeThirdPersonPluralForm .
    ?imperativeThirdPersonPluralForm ontolex:representation ?imperativeThirdPersonPlural ;
      wikibase:grammaticalFeature wd:Q22716, wd:Q51929074, wd:Q146786 .
  }
}
